[case testForInRange]
def f() -> None:
    x = 0
    for i in range(5):
        x = x + i
[out]
def f():
    x :: int
    r0 :: short_int
    i :: int
    r1 :: bool
    r2 :: int
    r3 :: short_int
    r4 :: None
L0:
    x = 0
    r0 = 0
    i = r0
L1:
    r1 = r0 < 10 :: signed
    if r1 goto L2 else goto L4 :: bool
L2:
    r2 = CPyTagged_Add(x, i)
    x = r2
L3:
    r3 = r0 + 2
    r0 = r3
    i = r3
    goto L1
L4:
    r4 = None
    return r4

[case testForInNegativeRange]
def f() -> None:
    for i in range(10, 0, -1):
        pass
[out]
def f():
    r0 :: short_int
    i :: int
    r1 :: bool
    r2 :: short_int
    r3 :: None
L0:
    r0 = 20
    i = r0
L1:
    r1 = r0 > 0 :: signed
    if r1 goto L2 else goto L4 :: bool
L2:
L3:
    r2 = r0 + -2
    r0 = r2
    i = r2
    goto L1
L4:
    r3 = None
    return r3

[case testBreak]
def f() -> None:
  n = 0
  while n < 5:
      break
[out]
def f():
    n :: int
    r0 :: bool
    r1 :: native_int
    r2 :: bool
    r3 :: native_int
    r4, r5, r6, r7 :: bool
    r8 :: None
L0:
    n = 0
L1:
    r1 = n & 1
    r2 = r1 == 0
    r3 = 10 & 1
    r4 = r3 == 0
    r5 = r2 & r4
    if r5 goto L2 else goto L3 :: bool
L2:
    r6 = n < 10 :: signed
    r0 = r6
    goto L4
L3:
    r7 = CPyTagged_IsLt_(n, 10)
    r0 = r7
L4:
    if r0 goto L5 else goto L6 :: bool
L5:
L6:
    r8 = None
    return r8

[case testBreakFor]
def f() -> None:
    for n in range(5):
        break
[out]
def f():
    r0 :: short_int
    n :: int
    r1 :: bool
    r2 :: short_int
    r3 :: None
L0:
    r0 = 0
    n = r0
L1:
    r1 = r0 < 10 :: signed
    if r1 goto L2 else goto L4 :: bool
L2:
    goto L4
L3:
    r2 = r0 + 2
    r0 = r2
    n = r2
    goto L1
L4:
    r3 = None
    return r3

[case testBreakNested]
def f() -> None:
    n = 0
    while n < 5:
        while n < 4:
            break
        break
[out]
def f():
    n :: int
    r0 :: bool
    r1 :: native_int
    r2 :: bool
    r3 :: native_int
    r4, r5, r6, r7 :: bool
    r8 :: bool
    r9 :: native_int
    r10 :: bool
    r11 :: native_int
    r12, r13, r14, r15 :: bool
    r16 :: None
L0:
    n = 0
L1:
    r1 = n & 1
    r2 = r1 == 0
    r3 = 10 & 1
    r4 = r3 == 0
    r5 = r2 & r4
    if r5 goto L2 else goto L3 :: bool
L2:
    r6 = n < 10 :: signed
    r0 = r6
    goto L4
L3:
    r7 = CPyTagged_IsLt_(n, 10)
    r0 = r7
L4:
    if r0 goto L5 else goto L12 :: bool
L5:
L6:
    r9 = n & 1
    r10 = r9 == 0
    r11 = 8 & 1
    r12 = r11 == 0
    r13 = r10 & r12
    if r13 goto L7 else goto L8 :: bool
L7:
    r14 = n < 8 :: signed
    r8 = r14
    goto L9
L8:
    r15 = CPyTagged_IsLt_(n, 8)
    r8 = r15
L9:
    if r8 goto L10 else goto L11 :: bool
L10:
L11:
L12:
    r16 = None
    return r16

[case testContinue]
def f() -> None:
  n = 0
  while n < 5:
      continue
[out]
def f():
    n :: int
    r0 :: bool
    r1 :: native_int
    r2 :: bool
    r3 :: native_int
    r4, r5, r6, r7 :: bool
    r8 :: None
L0:
    n = 0
L1:
    r1 = n & 1
    r2 = r1 == 0
    r3 = 10 & 1
    r4 = r3 == 0
    r5 = r2 & r4
    if r5 goto L2 else goto L3 :: bool
L2:
    r6 = n < 10 :: signed
    r0 = r6
    goto L4
L3:
    r7 = CPyTagged_IsLt_(n, 10)
    r0 = r7
L4:
    if r0 goto L5 else goto L6 :: bool
L5:
    goto L1
L6:
    r8 = None
    return r8

[case testContinueFor]
def f() -> None:
    for n in range(5):
        continue
[out]
def f():
    r0 :: short_int
    n :: int
    r1 :: bool
    r2 :: short_int
    r3 :: None
L0:
    r0 = 0
    n = r0
L1:
    r1 = r0 < 10 :: signed
    if r1 goto L2 else goto L4 :: bool
L2:
L3:
    r2 = r0 + 2
    r0 = r2
    n = r2
    goto L1
L4:
    r3 = None
    return r3

[case testContinueNested]
def f() -> None:
    n = 0
    while n < 5:
        while n < 4:
            continue
        continue
[out]
def f():
    n :: int
    r0 :: bool
    r1 :: native_int
    r2 :: bool
    r3 :: native_int
    r4, r5, r6, r7 :: bool
    r8 :: bool
    r9 :: native_int
    r10 :: bool
    r11 :: native_int
    r12, r13, r14, r15 :: bool
    r16 :: None
L0:
    n = 0
L1:
    r1 = n & 1
    r2 = r1 == 0
    r3 = 10 & 1
    r4 = r3 == 0
    r5 = r2 & r4
    if r5 goto L2 else goto L3 :: bool
L2:
    r6 = n < 10 :: signed
    r0 = r6
    goto L4
L3:
    r7 = CPyTagged_IsLt_(n, 10)
    r0 = r7
L4:
    if r0 goto L5 else goto L12 :: bool
L5:
L6:
    r9 = n & 1
    r10 = r9 == 0
    r11 = 8 & 1
    r12 = r11 == 0
    r13 = r10 & r12
    if r13 goto L7 else goto L8 :: bool
L7:
    r14 = n < 8 :: signed
    r8 = r14
    goto L9
L8:
    r15 = CPyTagged_IsLt_(n, 8)
    r8 = r15
L9:
    if r8 goto L10 else goto L11 :: bool
L10:
    goto L6
L11:
    goto L1
L12:
    r16 = None
    return r16

[case testForList]
from typing import List

def f(ls: List[int]) -> int:
    y = 0
    for x in ls:
        y = y + x
    return y
[out]
def f(ls):
    ls :: list
    y :: int
    r0, r1 :: short_int
    r2 :: bool
    r3 :: object
    x, r4, r5 :: int
    r6 :: short_int
L0:
    y = 0
    r0 = 0
L1:
    r1 = len ls :: list
    r2 = r0 < r1 :: signed
    if r2 goto L2 else goto L4 :: bool
L2:
    r3 = ls[r0] :: unsafe list
    r4 = unbox(int, r3)
    x = r4
    r5 = CPyTagged_Add(y, x)
    y = r5
L3:
    r6 = r0 + 2
    r0 = r6
    goto L1
L4:
    return y

[case testForDictBasic]
from typing import Dict

def f(d: Dict[int, int]) -> None:
    for key in d:
        d[key]
[out]
def f(d):
    d :: dict
    r0 :: short_int
    r1 :: int
    r2 :: object
    r3 :: tuple[bool, int, object]
    r4 :: int
    r5 :: bool
    r6 :: object
    key, r7 :: int
    r8, r9 :: object
    r10 :: int
    r11, r12 :: bool
    r13 :: None
L0:
    r0 = 0
    r1 = len d :: dict
    r2 = CPyDict_GetKeysIter(d)
L1:
    r3 = CPyDict_NextKey(r2, r0)
    r4 = r3[1]
    r0 = r4
    r5 = r3[0]
    if r5 goto L2 else goto L4 :: bool
L2:
    r6 = r3[2]
    r7 = unbox(int, r6)
    key = r7
    r8 = box(int, key)
    r9 = CPyDict_GetItem(d, r8)
    r10 = unbox(int, r9)
L3:
    r11 = CPyDict_CheckSize(d, r1)
    goto L1
L4:
    r12 = CPy_NoErrOccured()
L5:
    r13 = None
    return r13

[case testForDictContinue]
from typing import Dict

def sum_over_even_values(d: Dict[int, int]) -> int:
    s = 0
    for key in d:
        if d[key] % 2:
            continue
        s = s + d[key]
    return s
[out]
def sum_over_even_values(d):
    d :: dict
    s :: int
    r0 :: short_int
    r1 :: int
    r2 :: object
    r3 :: tuple[bool, int, object]
    r4 :: int
    r5 :: bool
    r6 :: object
    key, r7 :: int
    r8, r9 :: object
    r10 :: int
    r11 :: int
    r12 :: bool
    r13 :: native_int
    r14, r15, r16, r17 :: bool
    r18, r19 :: object
    r20, r21 :: int
    r22, r23 :: bool
L0:
    s = 0
    r0 = 0
    r1 = len d :: dict
    r2 = CPyDict_GetKeysIter(d)
L1:
    r3 = CPyDict_NextKey(r2, r0)
    r4 = r3[1]
    r0 = r4
    r5 = r3[0]
    if r5 goto L2 else goto L9 :: bool
L2:
    r6 = r3[2]
    r7 = unbox(int, r6)
    key = r7
    r8 = box(int, key)
    r9 = CPyDict_GetItem(d, r8)
    r10 = unbox(int, r9)
    r11 = CPyTagged_Remainder(r10, 4)
    r13 = r11 & 1
    r14 = r13 == 0
    if r14 goto L3 else goto L4 :: bool
L3:
    r15 = r11 != 0
    r12 = r15
    goto L5
L4:
    r16 = CPyTagged_IsEq_(r11, 0)
    r17 = !r16
    r12 = r17
L5:
    if r12 goto L6 else goto L7 :: bool
L6:
    goto L8
L7:
    r18 = box(int, key)
    r19 = CPyDict_GetItem(d, r18)
    r20 = unbox(int, r19)
    r21 = CPyTagged_Add(s, r20)
    s = r21
L8:
    r22 = CPyDict_CheckSize(d, r1)
    goto L1
L9:
    r23 = CPy_NoErrOccured()
L10:
    return s

[case testMultipleAssignment]
from typing import Tuple, Any

def from_tuple(t: Tuple[int, str]) -> None:
    x, y = t

def from_any(a: Any) -> None:
    x, y = a
[out]
def from_tuple(t):
    t :: tuple[int, str]
    x :: int
    y :: str
    r0 :: int
    r1 :: str
    r2 :: None
L0:
    r0 = t[0]
    x = r0
    r1 = t[1]
    y = r1
    r2 = None
    return r2
def from_any(a):
    a, x, y, r0, r1 :: object
    r2 :: bool
    r3 :: object
    r4 :: bool
    r5 :: object
    r6 :: bool
    r7 :: None
L0:
    r0 = iter a :: object
    r1 = next r0 :: object
    if is_error(r1) goto L1 else goto L2
L1:
    raise ValueError('not enough values to unpack')
    unreachable
L2:
    x = r1
    r3 = next r0 :: object
    if is_error(r3) goto L3 else goto L4
L3:
    raise ValueError('not enough values to unpack')
    unreachable
L4:
    y = r3
    r5 = next r0 :: object
    if is_error(r5) goto L6 else goto L5
L5:
    raise ValueError('too many values to unpack')
    unreachable
L6:
    r7 = None
    return r7

[case testMultiAssignmentCoercions]
from typing import Tuple, Any

def from_tuple(t: Tuple[int, Any]) -> None:
    x: object
    y: int
    x, y = t

def from_any(a: Any) -> None:
    x: int
    x, y = a
[out]
def from_tuple(t):
    t :: tuple[int, object]
    x :: object
    y, r0 :: int
    r1, r2 :: object
    r3 :: int
    r4 :: None
L0:
    r0 = t[0]
    r1 = box(int, r0)
    x = r1
    r2 = t[1]
    r3 = unbox(int, r2)
    y = r3
    r4 = None
    return r4
def from_any(a):
    a :: object
    x :: int
    y, r0, r1 :: object
    r2 :: bool
    r3 :: int
    r4 :: object
    r5 :: bool
    r6 :: object
    r7 :: bool
    r8 :: None
L0:
    r0 = iter a :: object
    r1 = next r0 :: object
    if is_error(r1) goto L1 else goto L2
L1:
    raise ValueError('not enough values to unpack')
    unreachable
L2:
    r3 = unbox(int, r1)
    x = r3
    r4 = next r0 :: object
    if is_error(r4) goto L3 else goto L4
L3:
    raise ValueError('not enough values to unpack')
    unreachable
L4:
    y = r4
    r6 = next r0 :: object
    if is_error(r6) goto L6 else goto L5
L5:
    raise ValueError('too many values to unpack')
    unreachable
L6:
    r8 = None
    return r8

[case testMultiAssignmentNested]
from typing import Tuple, Any, List

class A:
    x: int

def multi_assign(t: Tuple[int, Tuple[str, Any]], a: A, l: List[str]) -> None:
    z: int
    a.x, (l[0], z) = t
[out]
def multi_assign(t, a, l):
    t :: tuple[int, tuple[str, object]]
    a :: __main__.A
    l :: list
    z :: int
    r0 :: int
    r1 :: bool
    r2 :: tuple[str, object]
    r3 :: str
    r4 :: bool
    r5 :: object
    r6 :: int
    r7 :: None
L0:
    r0 = t[0]
    a.x = r0; r1 = is_error
    r2 = t[1]
    r3 = r2[0]
    r4 = CPyList_SetItem(l, 0, r3)
    r5 = r2[1]
    r6 = unbox(int, r5)
    z = r6
    r7 = None
    return r7

[case testAssert]
from typing import Optional

def no_msg(x: bool) -> int:
    assert x
    return 1

def literal_msg(x: object) -> int:
    assert x, 'message'
    return 2

def complex_msg(x: Optional[str], s: str) -> None:
    assert x, s
[out]
def no_msg(x):
    x, r0 :: bool
L0:
    if x goto L2 else goto L1 :: bool
L1:
    raise AssertionError
    unreachable
L2:
    return 2
def literal_msg(x):
    x :: object
    r0, r1 :: bool
L0:
    r0 = bool x :: object
    if r0 goto L2 else goto L1 :: bool
L1:
    raise AssertionError('message')
    unreachable
L2:
    return 4
def complex_msg(x, s):
    x :: union[str, None]
    s :: str
    r0 :: object
    r1 :: bool
    r2 :: str
    r3 :: bool
    r4 :: object
    r5 :: str
    r6, r7 :: object
    r8 :: None
L0:
    r0 = builtins.None :: object
    r1 = x is not r0
    if r1 goto L1 else goto L2 :: bool
L1:
    r2 = cast(str, x)
    r3 = bool r2 :: object
    if r3 goto L3 else goto L2 :: bool
L2:
    r4 = builtins :: module
    r5 = unicode_3 :: static  ('AssertionError')
    r6 = getattr r4, r5
    r7 = py_call(r6, s)
    CPy_Raise(r7)
    unreachable
L3:
    r8 = None
    return r8

[case testDelList]
def delList() -> None:
    l = [1, 2]
    del l[1]
def delListMultiple() -> None:
    l = [1, 2, 3, 4, 5, 6, 7]
    del l[1], l[2], l[3]
[out]
def delList():
    r0, r1 :: object
    r2, l :: list
    r3 :: object
    r4 :: bool
    r5 :: None
L0:
    r0 = box(short_int, 2)
    r1 = box(short_int, 4)
    r2 = [r0, r1]
    l = r2
    r3 = box(short_int, 2)
    r4 = l.__delitem__(r3) :: object
    r5 = None
    return r5
def delListMultiple():
    r0, r1, r2, r3, r4, r5, r6 :: object
    r7, l :: list
    r8 :: object
    r9 :: bool
    r10 :: object
    r11 :: bool
    r12 :: object
    r13 :: bool
    r14 :: None
L0:
    r0 = box(short_int, 2)
    r1 = box(short_int, 4)
    r2 = box(short_int, 6)
    r3 = box(short_int, 8)
    r4 = box(short_int, 10)
    r5 = box(short_int, 12)
    r6 = box(short_int, 14)
    r7 = [r0, r1, r2, r3, r4, r5, r6]
    l = r7
    r8 = box(short_int, 2)
    r9 = l.__delitem__(r8) :: object
    r10 = box(short_int, 4)
    r11 = l.__delitem__(r10) :: object
    r12 = box(short_int, 6)
    r13 = l.__delitem__(r12) :: object
    r14 = None
    return r14

[case testDelDict]
def delDict() -> None:
    d = {"one":1, "two":2}
    del d["one"]
def delDictMultiple() -> None:
    d = {"one":1, "two":2, "three":3, "four":4}
    del d["one"], d["four"]
[out]
def delDict():
    r0 :: str
    r1 :: str
    r2, r3 :: object
    r4, d :: dict
    r5 :: str
    r6 :: bool
    r7 :: None
L0:
    r0 = unicode_1 :: static  ('one')
    r1 = unicode_2 :: static  ('two')
    r2 = box(short_int, 2)
    r3 = box(short_int, 4)
    r4 = CPyDict_Build(2, r0, r2, r1, r3)
    d = r4
    r5 = unicode_1 :: static  ('one')
    r6 = d.__delitem__(r5) :: object
    r7 = None
    return r7
def delDictMultiple():
    r0 :: str
    r1 :: str
    r2 :: str
    r3 :: str
    r4, r5, r6, r7 :: object
    r8, d :: dict
    r9, r10 :: str
    r11, r12 :: bool
    r13 :: None
L0:
    r0 = unicode_1 :: static  ('one')
    r1 = unicode_2 :: static  ('two')
    r2 = unicode_3 :: static  ('three')
    r3 = unicode_4 :: static  ('four')
    r4 = box(short_int, 2)
    r5 = box(short_int, 4)
    r6 = box(short_int, 6)
    r7 = box(short_int, 8)
    r8 = CPyDict_Build(4, r0, r4, r1, r5, r2, r6, r3, r7)
    d = r8
    r9 = unicode_1 :: static  ('one')
    r10 = unicode_4 :: static  ('four')
    r11 = d.__delitem__(r9) :: object
    r12 = d.__delitem__(r10) :: object
    r13 = None
    return r13

[case testDelAttribute]
class Dummy():
    def __init__(self, x: int, y: int) -> None:
        self.x = x
        self.y = y
def delAttribute() -> None:
    dummy = Dummy(1, 2)
    del dummy.x
def delAttributeMultiple() -> None:
    dummy = Dummy(1, 2)
    del dummy.x, dummy.y
[out]
def Dummy.__init__(self, x, y):
    self :: __main__.Dummy
    x, y :: int
    r0, r1 :: bool
    r2 :: None
L0:
    self.x = x; r0 = is_error
    self.y = y; r1 = is_error
    r2 = None
    return r2
def delAttribute():
    r0, dummy :: __main__.Dummy
    r1 :: str
    r2 :: bool
    r3 :: None
L0:
    r0 = Dummy(2, 4)
    dummy = r0
    r1 = unicode_3 :: static  ('x')
    r2 = delattr dummy, r1
    r3 = None
    return r3
def delAttributeMultiple():
    r0, dummy :: __main__.Dummy
    r1 :: str
    r2 :: bool
    r3 :: str
    r4 :: bool
    r5 :: None
L0:
    r0 = Dummy(2, 4)
    dummy = r0
    r1 = unicode_3 :: static  ('x')
    r2 = delattr dummy, r1
    r3 = unicode_4 :: static  ('y')
    r4 = delattr dummy, r3
    r5 = None
    return r5

[case testForEnumerate]
from typing import List, Iterable

def f(a: List[int]) -> None:
    for i, x in enumerate(a):
        i + x
def g(x: Iterable[int]) -> None:
    for i, n in enumerate(x):
        pass
[out]
def f(a):
    a :: list
    r0 :: short_int
    i :: int
    r1, r2 :: short_int
    r3 :: bool
    r4 :: object
    x, r5, r6 :: int
    r7, r8 :: short_int
    r9 :: None
L0:
    r0 = 0
    i = 0
    r1 = 0
L1:
    r2 = len a :: list
    r3 = r1 < r2 :: signed
    if r3 goto L2 else goto L4 :: bool
L2:
    r4 = a[r1] :: unsafe list
    r5 = unbox(int, r4)
    x = r5
    r6 = CPyTagged_Add(i, x)
L3:
    r7 = r0 + 2
    r0 = r7
    i = r7
    r8 = r1 + 2
    r1 = r8
    goto L1
L4:
L5:
    r9 = None
    return r9
def g(x):
    x :: object
    r0 :: short_int
    i :: int
    r1, r2 :: object
    r3, n :: int
    r4 :: short_int
    r5 :: bool
    r6 :: None
L0:
    r0 = 0
    i = 0
    r1 = iter x :: object
L1:
    r2 = next r1 :: object
    if is_error(r2) goto L4 else goto L2
L2:
    r3 = unbox(int, r2)
    n = r3
L3:
    r4 = r0 + 2
    r0 = r4
    i = r4
    goto L1
L4:
    r5 = CPy_NoErrOccured()
L5:
    r6 = None
    return r6

[case testForZip]
from typing import List, Iterable

def f(a: List[int], b: Iterable[bool]) -> None:
    for x, y in zip(a, b):
        if b:
            x = 1

def g(a: Iterable[bool], b: List[int]) -> None:
    for x, y, z in zip(a, b, range(5)):
        x = False
[out]
def f(a, b):
    a :: list
    b :: object
    r0 :: short_int
    r1 :: object
    r2 :: short_int
    r3 :: bool
    r4, r5 :: object
    x, r6 :: int
    r7, y, r8 :: bool
    r9 :: short_int
    r10 :: bool
    r11 :: None
L0:
    r0 = 0
    r1 = iter b :: object
L1:
    r2 = len a :: list
    r3 = r0 < r2 :: signed
    if r3 goto L2 else goto L7 :: bool
L2:
    r4 = next r1 :: object
    if is_error(r4) goto L7 else goto L3
L3:
    r5 = a[r0] :: unsafe list
    r6 = unbox(int, r5)
    x = r6
    r7 = unbox(bool, r4)
    y = r7
    r8 = bool b :: object
    if r8 goto L4 else goto L5 :: bool
L4:
    x = 2
L5:
L6:
    r9 = r0 + 2
    r0 = r9
    goto L1
L7:
    r10 = CPy_NoErrOccured()
L8:
    r11 = None
    return r11
def g(a, b):
    a :: object
    b :: list
    r0 :: object
    r1, r2 :: short_int
    z :: int
    r3 :: object
    r4 :: short_int
    r5, r6, r7, x :: bool
    r8 :: object
    y, r9 :: int
    r10 :: bool
    r11, r12 :: short_int
    r13 :: bool
    r14 :: None
L0:
    r0 = iter a :: object
    r1 = 0
    r2 = 0
    z = r2
L1:
    r3 = next r0 :: object
    if is_error(r3) goto L6 else goto L2
L2:
    r4 = len b :: list
    r5 = r1 < r4 :: signed
    if r5 goto L3 else goto L6 :: bool
L3:
    r6 = r2 < 10 :: signed
    if r6 goto L4 else goto L6 :: bool
L4:
    r7 = unbox(bool, r3)
    x = r7
    r8 = b[r1] :: unsafe list
    r9 = unbox(int, r8)
    y = r9
    r10 = False
    x = r10
L5:
    r11 = r1 + 2
    r1 = r11
    r12 = r2 + 2
    r2 = r12
    z = r12
    goto L1
L6:
    r13 = CPy_NoErrOccured()
L7:
    r14 = None
    return r14

